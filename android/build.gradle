apply plugin: 'android'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        release {
            storeFile file("../../tba-keys.jks")
            keyAlias "thebluealliance-android"

            storePassword "notRealPassword"
            keyPassword "notRealPassword"
        }
    }
    buildTypes {

        debug {
            versionNameSuffix "-#" + gitSha
            packageNameSuffix ".development"
        }


        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                def file = variant.outputFile
                variant.outputFile = new File(file.parent, "tba-android-v" + defaultConfig.versionName + ".apk")
            }
        }
    }
}

task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("\nKeystore password: "))
    def keyPw = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}

dependencies {
    compile 'com.android.support:support-v13:19.+'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.android.support:support-v4:19.+'
    compile 'com.google.code.gson:gson:2.2.4'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
